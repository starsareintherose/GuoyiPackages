# Maintainer: Linus Dierheimer <Linus@Dierheimer.de>
pkgname=fastfetch
pkgver=1.2.0
pkgrel=6
pkgdesc="Like neofetch, but much faster because written in c"
arch=('x86_64')
url="https://github.com/LinusDierheimer/fastfetch"
license=('MIT')
depends=('glibc')
makedepends=(
  'git'
  'cmake'
  'pciutils'
  'libxrandr' # Depends on libX11, which headers are also needed
  'dconf'     # Depends on glib2, which headers are also needed
  'wayland'
  'sqlite'
)
optdepends=(
  "bash-completion"
  "pciutils: GPU output"
  "libx11: Basic resolution output"
  "libxrandr: Multi monitor and refresh rate support for resolution output"
  "wayland: Native wayland support for resolution output"
  "glib2: Output for values that are only stored in GSettings"
  "dconf: Output for values that are only stored in DConf"
  "xfconf: XFWM theme + xfce-terminal font"
  "sqlite: rpm package count"
)

provides=('fastfetch')
conflicts=("fastfetch-git")

source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('04ca89a9f18364ce3eedeb7fc0ea5a9397247895c931a73e6e694b962207ce6a')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build --target "$pkgname"
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
