# Maintainer: Linus Dierheimer <Linus@Dierheimer.de>
pkgname=fastfetch
pkgver=1.0.0
pkgrel=3
pkgdesc="Like neofetch, but much faster because written in c"
arch=('x86_64')
url="https://github.com/LinusDierheimer/fastfetch"
license=('MIT')
depends=('glibc')
makedepends=(
  'git'
  'cmake'
  'pciutils'
  'libxrandr' # Depends on libX11, which headers are also needed
  'dconf'     # Depends on glib2, which headers are also needed
  'wayland'
  'sqlite'
)
optdepends=(
  "bash-completion"
  "pciutils: GPU output"
  "libx11: Basic resolution output"
  "libxrandr: Multi monitor and refresh rate support for resolution output"
  "wayland: Native wayland support for resolution output"
  "glib2: Output for values that are only stored in GSettings"
  "dconf: Output for values that are only stored in DConf"
  "xfconf: XFWM theme + xfce-terminal font"
  "sqlite: rpm package count"
)

provides=('fastfetch')
conflicts=("fastfetch-git")

source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('316a6a86a7fcd5e521f10da1b4b766f964252d3a8324c77a80696c78cf5d6c01')

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build --target "$pkgname"
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
