# Maintainer: Astro Benzene <universebenzene at sina dot com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ouyang Jun <ouyangjun1999@gmail.com>
# Contributor: Jove Yu <yushijun110 [at] gmail.com>
# Contributor: Ariel AxionL <axionl at aosc dot io>

pkgbase=wps-office-cn
pkgname=('wps-office-mime-cn' 'wps-office-mui-zh-cn')
pkgver=11.1.0.10920
pkgrel=8
pkgdesc="Kingsoft Office (WPS Office) CN version - an office productivity suite"
arch=('x86_64' 'aarch64')
license=('custom')
url="https://linux.wps.cn"
options=('!emptydirs')
source=("https://wdl1.cache.wps.cn/wps/download/ep/Linux2019/${pkgver##*.}/wps-office_${pkgver}_amd64.deb")
sha1sums=('5748fec427c5c1dab2b356c2f058dcce22f15212')

prepare() {
    bsdtar -xpf data.tar.xz

    cd "${srcdir}/usr/bin"
    sed -i 's|/opt/kingsoft/wps-office|/usr/lib|' *
    [[ "$CARCH" = "aarch64" ]] && sed -i '2a export LD_PRELOAD=/usr/lib/libfreetype.so' *

    cd "${srcdir}/usr/share/icons/hicolor"
    for _file in ./*; do
        if [ -e ${_file}/mimetypes/wps-office2019-etmain.png ]; then
            mkdir -p ${_file}/apps
            cp -p ${_file}/mimetypes/wps-office2019* ${_file}/apps
        fi
    done
}

package_wps-office-mui-zh-cn() {
    pkgdesc="Chinese (Simplified) mui package for WPS Office"
    arch=('any')
    cd "${srcdir}/opt/kingsoft/wps-office/office6/mui"

    install -d "${pkgdir}/usr/lib/office6/mui/en_US/resource"
    cp -r en_US/resource/help "${pkgdir}/usr/lib/office6/mui/en_US/resource"
    cp -r zh_CN "${pkgdir}/usr/lib/office6/mui"

    cd "${srcdir}/opt/kingsoft/wps-office/"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" office6/mui/default/*.html
}

package_wps-office-mime-cn() {
    pkgdesc="Mime files provided by Kingsoft Office (WPS Office) cn version"
    arch=('any')
    depends=('shared-mime-info')
    conflicts=('wps-office-mime')
    provides=('wps-office-mime')
    cd "${srcdir}/usr/share"

    install -d "${pkgdir}/usr/share/mime"
    cp -r mime/* "${pkgdir}/usr/share/mime"

    cd "${srcdir}/opt/kingsoft/wps-office/"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" office6/mui/default/*.html
}
